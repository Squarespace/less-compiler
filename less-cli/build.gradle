
apply plugin: 'application'

dependencies {
  compile project(':less2-core')
  compile 'net.sourceforge.argparse4j:argparse4j:0.4.4'

  testCompile project(path: ':less2-core', configuration: 'tests')
  testCompile 'org.testng:testng:6.8.7'
}

task execJar(type: Jar, dependsOn: classes) {
  classifier = 'exec'
  manifest {
    attributes 'Main-Class': 'com.squarespace.less.cli.LessC'
  }
  from files(sourceSets.main.output.classesDirs)
  from files(sourceSets.main.output.resourcesDir)
  from {
    configurations.runtime.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
}

mainClassName = 'com.squarespace.less.cli.LessC'

jar {
  manifest {
    attributes 'Main-Class': "${mainClassName}"
  }
}

task makeCli(type: Exec, dependsOn: execJar,
  description: 'Create executable "lessc" command') {

  def inputScript = project.file('src/main/resources/scripts/lessc.in')
  def inputArchive = execJar.archivePath
  def destScript = rootProject.file('lessc')
  workingDir '.'
  executable 'bash'
  args '-c', "cat ${inputScript} ${inputArchive} >${destScript}; chmod ug+x ${destScript}"
}

artifacts {
  archives sourcesJar
  archives javadocJar
  archives execJar
}

// Ensure Eclipse can see the test base classes from less-core
eclipse.classpath.file.whenMerged {
  entries.findAll { it instanceof org.gradle.plugins.ide.eclipse.model.ProjectDependency }
    .each { it.entryAttributes['without_test_code'] = 'false' }
}

